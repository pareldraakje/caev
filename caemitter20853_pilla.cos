**CAOS2PRAY
*# Pray-File "caemitter_pilla.agents"
*# DS-Name "CAev emitter vendor"
*# attach caemitter_pilla.catalogue caemitter_pilla.c16
*# desc = "This exotic creature injects little bubbles into your game which emit cellular automata (CA) in various strengths. Be sure to read the agents' help text!by Pilla."
*# Agent Animation File = "caemitter_pilla.c16"
*# Agent Sprite First Image = 0
*# Agent Animation Gallery = "caemitter_pilla"
*# Web URL = "Creaturescaves.com"
*# Web Label = "CreaturesCaves"



** Injection part
**

new: comp 2 23 20853 "caemitter_pilla" 1 0 1500
mesg wrt+ targ 1000 game "CreatorX" game "CreatorY" 1


** Script part
**

** Emitter vendor vending script

scrp 2 23 20853 600
** Set OV10: the CA emitted
** set OV11: the starting frame for the CA emitter
	doif _p1_ ne 21
		setv va10 _p1_
		subv va10 10

		setv va11 _p1_
		addv va11 31
	else
		setv va10 18
		setv va11 52
	endi

** Set coordinates to spawn
	setv va00 posl
	addv va00 86
	setv va01 post
	addv va01 20

** remember the emitter type
	setv va02 ov10

** Move out of beak!
	new: comp 1 1 20853 "caemitter_pilla" 1 va11 rand 3500 3600
	setv ov00 va10
	setv ov01 2
	mesg wrt+ targ 1000 va00 va01 1
endm

** Emitter creation script
scrp 1 1 20853 1000
* attr: mouseable 2 + activateable 4 + invisible 16 + collisions 64 + physics 128 = 214
	attr 214
	bhvr 0

	accg 0
	fric 100
	elas 0
	aero 10

	fdmp 0.8
	sdmp 0.8
	mvsf _p1_ _p2_
	velo rand 1 4 rand -2 -6
* Part 2: Disable button 
	pat: butt 2 "caemitter_pilla" 31 2 8 0 2 [] 600 1
* Part 3: Gauge
	pat: dull 3 "caemitter_pilla" 33 0 0 1

* Set as activated
	part 2
	pose 1
* Set gauge
	part 3
	pose ov01

	doif ov01 eq 0
		setv ov02 0
	elif ov01 eq 1
		setv ov02 0.05
	elif ov01 eq 2
		setv ov02 0.1
	elif ov01 eq 3
		setv ov02 0.2
	elif ov01 eq 4
		setv ov02 0.5
	elif ov01 eq 5
		setv ov02 0.9
	elif ov01 eq 6
		setv ov02 1
	else
		setv ov02 2
	endi


	emit ov00 ov02



endm

** Disable button

scrp 1 1 20853 600

** Check the pose of the activate button
	part 2
	setv va00 pose

	doif va00 eq 0
		pose 1
	else
		pose 0
	endi

	part 3
	doif va00 eq 0
		pose ov01
		emit ov00 ov02
	else
		pose 0
		emit ov00 0
	endi
endm

** Gauge increase OR emitter kill script

scrp 1 1 20853 1

* Check if Shift is being held. If yes, kill targ D:
	doif keyd 16 eq 1
		kill ownr
		stop
	endi

* Check if disabled
	part 2
	setv va00 pose
	doif va00 eq 0
		stop
	else

* If not disabled, increase the strength
		part 3

		addv ov01 1
		doif ov01 gt 7
			setv ov01 0
		endi

		pose ov01

		doif ov01 eq 0
			setv ov02 0
		elif ov01 eq 1
			setv ov02 0.05
		elif ov01 eq 2
			setv ov02 0.1
		elif ov01 eq 3
			setv ov02 0.2
		elif ov01 eq 4
			setv ov02 0.5
		elif ov01 eq 5
			setv ov02 0.9
		elif ov01 eq 6
			setv ov02 1
		else
			setv ov02 2
		endi

		emit ov00 ov02

	endi
endm







** Vendor creation script

scrp 2 23 20853 1000

* Attr: mouseable 2 + invisible 16 + collide 64 + physics 128 = 210
	attr 210
* Bhvr: 0 since creatures can't interact
	bhvr 0

* Collisions: Don't fall through floors.
	perm 100
* Physics: Heavy, not really elastic, very friction, normal aerodynamic resistance.
	accg 2
	elas 20
	fric 80
	aero 1

* Now the parts! 8D

* Part 10: Sound button
	pat: butt 10 "caemitter_pilla" 1 2 51 5 1 [1] 600 1
* Part 11: Light button
	pat: butt 11 "caemitter_pilla" 3 2 31 3 1 [1] 600 1
* Part 12: Heat button
	pat: butt 12 "caemitter_pilla" 5 2 14 9 1 [1] 600 1
* Part 13: Rain button
	pat: butt 13 "caemitter_pilla" 7 2 3 29 1 [1] 600 1
* Part 14: Nutrient button
	pat: butt 14 "caemitter_pilla" 9 2 3 49 1 [1] 600 1
* Part 15: Water button
	pat: butt 15 "caemitter_pilla" 11 2 0 66 1 [1] 600 1
* Part 16: Protein button
	pat: butt 16 "caemitter_pilla" 13 2 6 77 1 [1] 600 1
* Part 17: Carb button
	pat: butt 17 "caemitter_pilla" 15 2 22 77 1 [1] 600 1
* Part 18: Fat button
	pat: butt 18 "caemitter_pilla" 17 2 38 75 1 [1] 600 1
* Part 19: Flower button
	pat: butt 19 "caemitter_pilla" 19 2 55 74 1 [1] 600 1
* Part 20: Machinery button
	pat: butt 20 "caemitter_pilla" 21 2 71 76 1 [1] 600 1
* Part 21: Gadget button
	pat: butt 21 "caemitter_pilla" 23 2 86 74 1 [1] 600 1
* Part 22: Norn button
	pat: butt 22 "caemitter_pilla" 25 1 30 28 1 [] 600 1
* Part 23: Grendel button
	pat: butt 23 "caemitter_pilla" 26 1 29 42 1 [] 600 1
* Part 24: Ettin button
	pat: butt 24 "caemitter_pilla" 27 1 30 58 1 [] 600 1
* Part 25: Norn home button
	pat: butt 25 "caemitter_pilla" 28 1 51 27 1 [] 600 1
* Part 26: Grendel home button
	pat: butt 26 "caemitter_pilla" 29 1 51 42 1 [] 600 1
* Part 27: Ettin home button
	pat: butt 27 "caemitter_pilla" 30 1 51 57 1 [] 600 1

** animate the parts
	part 10
	anim [0]
	part 11
	anim [0]
	part 12
	anim [0]
	part 13
	anim [0]
	part 14
	anim [0]
	part 15
	anim [0]
	part 16
	anim [0]
	part 17
	anim [0]
	part 18
	anim [0]
	part 19
	anim [0]
	part 20
	anim [0]
	part 21
	anim [0]

	mvsf _p1_ _p2_
endm


** Removal script
rscr
enum 2 23 20853
	kill targ
next
enum 1 1 20853
	kill targ
next